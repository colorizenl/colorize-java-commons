plugins {
    id "io.freefair.lombok" version "8.4"
    id "com.github.ben-manes.versions" version "0.51.0"
}

apply plugin: "java-library"
apply plugin: "jacoco"
apply plugin: "maven-publish"
apply plugin: "signing"

group = "nl.colorize"
version = "2024.2"
compileJava.options.encoding = "UTF-8"

java {
    sourceCompatibility = "21"
    targetCompatibility = "21"
    sourceSets.main.java.srcDirs = ["source"]
    sourceSets.main.resources.srcDirs = ["resources"]
    sourceSets.test.java.srcDirs = ["test"]
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api "com.google.guava:guava:33.0.0-jre"
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.1"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["**/swing/**", "**/Platform*"])
        })
    }
}

task coverage(dependsOn: ["test", "jacocoTestReport"]) {
    doLast {
        def report = file("${buildDir}/reports/jacoco/test/html/index.html").text
        def coverage = report =~ "tfoot.*?ctr2.*?>([\\d.]+%).*?tfoot"
        println "-" * 40
        println "Test coverage: " + coverage[0][1]
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "colorize-java-commons"
            from components.java

            pom {
                name = "Colorize-Java-Commons"
                description = "Library containing utility classes for web, desktop, and mobile applications."
                url = "https://www.clrz.nl"

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "colorize"
                        name = "Colorize"
                        email = "contact@colorize.nl"
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/colorizenl/colorize-java-commons.git"
                    developerConnection = "scm:git:https://github.com/colorizenl/colorize-java-commons.git"
                    url = "https://github.com/colorizenl/colorize-java-commons"
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username mavenCentralUser
                password mavenCentralPassword
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
