plugins {
    id "io.freefair.lombok" version "8.14"
    id "com.vanniktech.maven.publish" version "0.34.0"
    id "com.github.ben-manes.versions" version "0.52.0"
}

apply plugin: "java-library"
apply plugin: "jacoco"

group = "nl.colorize"
version = "2025.2"
compileJava.options.encoding = "UTF-8"

java {
    sourceCompatibility = "21"
    targetCompatibility = "21"
    sourceSets.main.java.srcDirs = ["source"]
    sourceSets.main.resources.srcDirs = ["resources"]
    sourceSets.test.java.srcDirs = ["test"]
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.jspecify:jspecify:1.0.0"
    api "com.google.guava:guava:33.4.8-jre"
    testImplementation "org.junit.jupiter:junit-jupiter:5.13.2"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["**/swing/**", "**/Platform*"])
        })
    }
}

task coverage(dependsOn: ["test", "jacocoTestReport"]) {
    doLast {
        def report = file("${buildDir}/reports/jacoco/test/html/index.html").text
        def coverage = report =~ "tfoot.*?ctr2.*?>([\\d.]+%).*?tfoot"
        println "-" * 40
        println "Test coverage: " + coverage[0][1]
    }
}

mavenPublishing {
    coordinates("nl.colorize", "colorize-java-commons", version)

    pom {
        name = "Colorize-Java-Commons"
        description = "Library containing utility classes for web, desktop, and mobile applications."
        url = "https://www.clrz.nl"
        inceptionYear = "2007"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
            developers {
                developer {
                    id = "colorize"
                    name = "Colorize"
                    url = "https://clrz.nl"
                }
            }
            scm {
                url = "https://github.com/colorizenl/colorize-java-commons"
                connection = "scm:git:https://github.com/colorizenl/colorize-java-commons.git"
                developerConnection = "scm:git:https://github.com/colorizenl/colorize-java-commons.git"
            }
        }
    }
}
